apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = "de.westnordost"
archivesBaseName = "osmapi-overpass"
version = 3.0
description = 'Overpass API client - Querying map data from the overpass API and checking request quota'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	compile 'de.westnordost:osmapi-core:3.0'
	compile 'de.westnordost:osmapi-map:3.0'
	compile 'org.jetbrains:annotations-java5:18.0.0'
	testCompile 'junit:junit:4.13.2'
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name archivesBaseName
				packaging 'jar'
				description description
				url 'https://github.com/westnordost/osmapi-overpass'

				scm {
					connection 'https://github.com/westnordost/osmapi-overpass.git'
					developerConnection 'https://github.com/westnordost/osmapi-overpass.git'
					url 'https://github.com/westnordost/osmapi-overpass'
				}

				licenses {
					license {
						name 'GNU Lesser General Public License, Version 3.0'
						url 'http://www.gnu.org/licenses/lgpl-3.0.html'
					}
				}

				developers {
					developer {
						id 'westnordost'
						name 'Tobias Zwick'
						email 'osm@westnordost.de'
					}
				}
			}
		}
	}
}


